plugins {
    id 'com.gradle.plugin-publish' version '0.17.0' apply false
    id 'org.gradle.test-retry' version '1.3.1' apply false
}

apply from: 'deps.gradle'

subprojects {
    group = 'xyz.tynn.buildsrc'

    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    plugins.withId('java-gradle-plugin') {
        project.apply plugin: 'com.gradle.plugin-publish'
        project.apply plugin: 'org.gradle.test-retry'

        sourceCompatibility = 11

        pluginBundle {
            website = 'https://github.com/tynn-xyz/BuildSrc/blob/master/README.md'
            vcsUrl = 'https://github.com/tynn-xyz/BuildSrc/'
        }

        tasks.named('testClasses') {
            dependsOn tasks.pluginUnderTestMetadata
        }

        test {
            forkEvery 5
            retry {
                maxFailures = 33
                maxRetries = 3
                failOnPassedAfterRetry = false
            }
        }
    }

    afterEvaluate {
        test {
            useJUnitPlatform()
            testLogging {
                events 'failed', 'passed'
                exceptionFormat 'full'
                showCauses true
                showExceptions true
                showStackTraces true
            }
        }
    }
}
