//  Copyright 2023 Christian Schmitz
//  SPDX-License-Identifier: Apache-2.0

if (parent == null) afterEvaluate {
    try {
        tasks.register('clean', Delete) {
            delete rootProject.buildDir
        }
    } catch (InvalidUserDataException ignored) {
    }
}

if (parent == null) {
    pluginManager.withPlugin('wrapper') {
        tasks.named('wrapper') {
            distributionType Wrapper.DistributionType.ALL
        }
    }
}

allprojects {
    pluginManager.with {
        withPlugin('maven-publish') {
            apply 'xyz.tynn.convention.release'
        }
        withPlugin('com.android.application') {
            apply 'xyz.tynn.convention.android'
        }
        withPlugin('com.android.library') {
            apply 'xyz.tynn.convention.android'
        }
        withPlugin('org.jetbrains.kotlin.android') {
            apply 'xyz.tynn.convention.kotlin'
        }
        withPlugin('org.jetbrains.kotlin.jvm') {
            apply 'xyz.tynn.convention.kotlin'
        }
        withPlugin('io.github.gradle-nexus.publish-plugin') {
            nexusPublishing.repositories.sonatype {
                final host = 'https://s01.oss.sonatype.org'
                nexusUrl.set uri("$host/service/local/")
                snapshotRepositoryUrl.set uri("$host/content/repositories/snapshots/")
            }
        }
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}
